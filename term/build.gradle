plugins {
    id 'com.android.application'
}

android {
    namespace "com.termoneplus"

    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion
    ndkVersion project.ext.ndkVersion
    defaultConfig {
        applicationId "com.termoneplus"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        vectorDrawables.useSupportLibrary = true

        versionCode 440
        versionName '4.4.0'

        externalNativeBuild {
            cmake {
                arguments "-DAPPLICATION_ID:STRING=${applicationId}"
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions "catalog"
    productFlavors {
        play {
            // Google Play store
            dimension 'catalog'
        }
        full {
            dimension 'catalog'
            versionName android.defaultConfig.versionName + "/X"
        }
    }
    if (project.hasProperty("signing.properties")) {

        Properties properties = new Properties()
        properties.load(new FileInputStream(file(project.property("signing.properties"))))

        signingConfigs {
            release {
                storeFile file(properties["store.file"])
                storePassword properties["store.password"]
                keyAlias properties["key.alias"]
                keyPassword properties["key.password"]
            }
        }

        android.buildTypes.release.signingConfig = signingConfigs.release
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    applicationVariants.all { variant ->
        variant.resValue  "string", "application_version", variant.versionName
    }
}

dependencies {
    implementation project(':emulatorview')
    implementation project(':libtermexec')

    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "com.google.android.material:material:1.9.0"
    implementation "androidx.preference:preference:1.2.0"
    implementation "androidx.annotation:annotation:1.6.0"
    constraints {
        /* upgrade annotation 1.5.0 -> 1.6.0
         ...
    Duplicate class kotlin.internal.jdk7.JDK7PlatformImplementations found in modules kotlin-stdlib-1.8.0 (org.jetbrains.kotlin:kotlin-stdlib:1.8.0) and kotlin-stdlib-jdk7-1.6.0 (org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.6.0)
    Duplicate class kotlin.internal.jdk8.JDK8PlatformImplementations found in modules kotlin-stdlib-1.8.0 (org.jetbrains.kotlin:kotlin-stdlib:1.8.0) and kotlin-stdlib-jdk8-1.6.0 (org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0)
         ...
         */
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

task elfcleaner(type: Exec) {
    // only unix
    commandLine 'sh', '-c', 'find build/*/stripped* -name lib*.so -print0 | xargs -0 -P 0 elf-cleaner'
}

tasks.whenTaskAdded { task ->
    if (task.name.matches('strip.*DebugSymbols')) {
        task.finalizedBy elfcleaner
    }
}
