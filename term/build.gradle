plugins {
    id 'com.android.application'
}

android {
    namespace "com.termoneplus"

    compileSdk rootProject.ext.compileSdk
    buildToolsVersion = rootProject.ext.buildToolsVersion
    ndkVersion rootProject.ext.ndkVersion
    defaultConfig {
        applicationId "com.termoneplus"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        vectorDrawables.useSupportLibrary = true

        versionCode 450
        versionName '4.5.0'

        externalNativeBuild {
            cmake {
                arguments "-DAPPLICATION_ID:STRING=${applicationId}"
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/jni/CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions = ["catalog"]
    productFlavors {
        play {
            // Google Play store
            dimension 'catalog'
        }
        full {
            dimension 'catalog'
            versionName android.defaultConfig.versionName + "/X"
        }
    }
    if (project.hasProperty("signing.properties")) {

        Properties properties = new Properties()
        properties.load(new FileInputStream(file(project.property("signing.properties"))))

        signingConfigs {
            release {
                storeFile file(properties["store.file"])
                storePassword properties["store.password"]
                keyAlias properties["key.alias"]
                keyPassword properties["key.password"]
            }
        }

        android.buildTypes.release.signingConfig = signingConfigs.release
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    applicationVariants.configureEach { variant ->
        variant.resValue "string", "application_version", variant.versionName
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }
}

dependencies {
    implementation project(':emulatorview')
    implementation project(':libtermexec')

    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "com.google.android.material:material:1.10.0"
    implementation "androidx.preference:preference:1.2.1"
    implementation "androidx.annotation:annotation:1.7.1"
}

tasks.register('elfcleaner', Exec) {
    // only unix
    commandLine 'sh', '-c', 'find build/*/stripped* -name lib*.so -print0 | xargs -0 -P 0 elf-cleaner'
}

tasks.configureEach { task ->
    if (task.name.matches('strip.*DebugSymbols')) {
        task.finalizedBy elfcleaner
    }
}
